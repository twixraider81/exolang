/*
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

header {
	#include "exo/exo.h"
}

start = EXO;

mode EXO:
<skip:       [ \r\n\t]>		/* skip whitespace */
<skip_range: "/*" "*/">		/* skip multiline comments */
<skip_range: "//" "\n">		/* skip singleline comments */
<skip_range: "#" "\n">		/* skip singleline comments */
{
	<<EOF>>							=> QUEX_TKN_TERMINATION;
	<<FAIL>>						{ ERRORMSG( "unknown token: " << Lexeme ); }

	"null"							=> QUEX_TKN_T_TNULL;
	"nil"							=> QUEX_TKN_T_TNULL;
	"bool"							=> QUEX_TKN_T_TBOOL;
	"boolean"						=> QUEX_TKN_T_TBOOL;
	"int"							=> QUEX_TKN_T_TINT;
	"integer"						=> QUEX_TKN_T_TINT;
	"float"							=> QUEX_TKN_T_TFLOAT;
	"string"						=> QUEX_TKN_T_TSTRING;
	"auto"							=> QUEX_TKN_T_TAUTO;
	"callable"						=> QUEX_TKN_T_TCALLABLE;
	"function"						=> QUEX_TKN_S_FUNCTION;

	";"								=> QUEX_TKN_S_SEMICOLON;
	","								=> QUEX_TKN_S_COMMA;

	"$"([_a-zA-Z\\]+[_a-zA-Z\\0-9]*) => QUEX_TKN_T_VAR(LexemeBegin+1,LexemeEnd);
	([_a-zA-Z\\]+[_a-zA-Z\\0-9]*)	=> QUEX_TKN_T_ID(Lexeme);

	"("								=> QUEX_TKN_S_LANGLE;
	")"								=> QUEX_TKN_S_RANGLE;
	"{"								=> QUEX_TKN_S_LBRACKET;
	"}"								=> QUEX_TKN_S_RBRACKET;

	[0-9]+							=> QUEX_TKN_T_VINT(Lexeme);
	([0-9]+[.,][0-9]*)				=> QUEX_TKN_T_VFLOAT(Lexeme);

	"="								=> QUEX_TKN_S_ASSIGN;

	"=="							=> QUEX_TKN_S_EQ;
	"!="							=> QUEX_TKN_S_NE;
	"<"								=> QUEX_TKN_S_LT;
	"<="							=> QUEX_TKN_S_LE;
	">"								=> QUEX_TKN_S_GT;
	">="							=> QUEX_TKN_S_GE;
	"+"								=> QUEX_TKN_S_PLUS;
	"-"								=> QUEX_TKN_S_MINUS;
	"*"								=> QUEX_TKN_S_MUL;
	"/"								=> QUEX_TKN_S_DIV;
}